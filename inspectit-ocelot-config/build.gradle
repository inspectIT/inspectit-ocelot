import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'maven-publish'
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManangementVersion}"
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

group = 'rocks.inspectit.ocelot'
sourceCompatibility = 1.8 // Java version compatibility to use when compiling Java source.
targetCompatibility = 1.8 // Java version to generate classes for.
dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}
dependencies {
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testImplementation (
            "org.junit.jupiter:junit-jupiter-api",
            "org.mockito:mockito-junit-jupiter",
            "org.assertj:assertj-core"
    )
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    implementation(
//            enforcedPlatform("com.fasterxml.jackson:jackson-bom:${jacksonBomVersion}"),
            "org.springframework.boot:spring-boot",
            "org.hibernate.validator:hibernate-validator",
            "org.apache.commons:commons-lang3",
            "commons-io:commons-io:${commonsIoVersion}",
            "com.fasterxml.jackson.core:jackson-databind",

            // logging
            "ch.qos.logback:logback-classic",
    )

    // the api is included as compile only because the open census api has to be decoupled
    // from the inspectIT core to allow it to be pushed to the bootstrap
    compileOnly (
            "io.opencensus:opencensus-api:${openCensusVersion}",
            // OpenTelemetry
            platform("io.opentelemetry:opentelemetry-bom:${openTelemetryVersion}"),
            "io.opentelemetry:opentelemetry-sdk-metrics",
    )
}
