plugins {
    id "me.champeau.gradle.jmh" version "0.4.7"
}

/**
 * JMH-Perf tests.
 */
jmhJar {
    doFirst {
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }
}
jmh {
    /**
     * Use -PjmhInclude='regular expression' to specify what tests to run with JMH.
     */
    if (project.hasProperty('jmhInclude')) {
        def regex = project.getProperty('jmhInclude')
        println "Filtering for JMH-Tests matching to regex " + regex
        include = [regex]
    }
    humanOutputFile = file("$buildDir/jmh/human.txt")
    resultsFile = file("$buildDir/jmh/results.txt")
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
    jmhVersion = '1.21'
}

configurations{
    opencensus
    testImplementation.extendsFrom opencensus
}

dependencies {
    compileOnly(
            project(':inspectit-oce-bootstrap'),
            "io.opencensus:opencensus-api:${openCensusVersion}"
    )

    opencensus(
            "io.opencensus:opencensus-impl:${openCensusVersion}"
    )

    implementation(
            // spring releated
            'org.springframework.boot:spring-boot:2.1.1.RELEASE',
            'org.yaml:snakeyaml:1.23',
            'javax.annotation:javax.annotation-api:1.3.2', //Required for @PostConstruct and @PreDestroy to work in Java9+

            // data validation
            'org.hibernate.validator:hibernate-validator:6.0.13.Final',
            'org.apache.tomcat.embed:tomcat-embed-el:9.0.13',

            // logging
            'ch.qos.logback:logback-classic:1.2.3',

            // utils
            "org.apache.commons:commons-lang3:3.+",
            'piccolo:piccolo:1.0.3',
            'com.fasterxml.jackson.core:jackson-databind:2.9.7',

            // OpenCensus exporters
            "io.opencensus:opencensus-exporter-stats-prometheus:${openCensusVersion}",
            "io.prometheus:simpleclient_httpserver:${prometheusClientVersion}",

            //bytecode manipulation
            "net.bytebuddy:byte-buddy:1.9.2"
    )

    testImplementation(
            project(':inspectit-oce-bootstrap'),
            'org.springframework:spring-test:5.1.3.RELEASE',
            'org.apache.httpcomponents:httpclient:4.5.6',
            'org.mockito:mockito-core:2.23.4',
            'org.assertj:assertj-core:3.11.1'
    )

}

task buildOpencensusFatJar(type: Jar) {
    archiveName = "opencensus-fat.jar"
    destinationDir = file("$buildDir/jarGen")
    from {
        configurations.opencensus.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jar {
    archiveName = "${project.name}.jar"

    //include the open-census dependencies as a fat jar
    dependsOn buildOpencensusFatJar
    from "$buildDir/jarGen"

    //include all "implementation" dependencies in fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
