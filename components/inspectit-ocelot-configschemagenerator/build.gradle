dependencies {
    testImplementation(
            configServerLibs.orgJunitJupiterJunitJupiter
    )

    implementation(
            project(':inspectit-ocelot-config'),
            libs.ioOpentelemetryOpentelemetryApi,

            // We chose this generator, because it has a great feature-set and is actively maintained
            configServerLibs.comGithubVictoolsJsonschemaGenerator,
            configServerLibs.comGithubVictoolsJsonschemaModuleJackson,
            configServerLibs.comGithubVictoolsJsonschemaModuleJavaxValidation,
    )

    compileOnly libs.orgProjectlombokLombok
    annotationProcessor libs.orgProjectlombokLombok
}

task generateConfigSchema(type: JavaExec) {
    def outputPath = "$buildDir/inspectit-ocelot-configuration-schema-${version}.json"

    classpath = sourceSets.main.runtimeClasspath
    mainClass.set("rocks.inspectit.ocelot.configschemagenerator.InspectitSchemaGenerator")
    args outputPath
}

// build fatJar with dependencies included
jar {
    manifest {
        attributes('Main-Class': 'rocks.inspectit.ocelot.configschemagenerator.InspectitSchemaGenerator')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}