plugins {
	id 'org.springframework.boot' version '2.1.4.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'rocks.inspectit.oce'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

task downloadBoomerangZipFile() {
	ext.dest = new File(buildDir, "boomerang-source-${boomerangVersion}.zip")
	outputs.files(ext.dest)
	doLast {
		def f = ext.dest
		new URL("https://github.com/akamai/boomerang/archive/${boomerangVersion}.zip")
				.withInputStream{ i -> f.withOutputStream{ it << i }}
	}
}

task downloadAndUnzipBoomerang(dependsOn: downloadBoomerangZipFile, type: Copy) {
	from zipTree(downloadBoomerangZipFile.dest)
	into project.buildDir
}

bootJar {
	dependsOn downloadAndUnzipBoomerang

	archivesBaseName = 'inspectit-ocelot-eum-server'
	version = "${buildVersion}"

	manifest {
		attributes 'Start-Class': 'rocks.inspectit.oce.eum.server.EUMServerApplication'
	}

	from ("$buildDir/boomerang-${boomerangVersion}"){
		include "plugins/*.js"
		include "boomerang.js"
		into "static/boomerang"
	}
}

test {
	useJUnitPlatform()

	testLogging {
		exceptionFormat = 'full'
	}
}

dependencies {
	implementation(
			project(':inspectit-ocelot-config'),
			"org.springframework.boot:spring-boot-starter-web",
			"org.springframework.security:spring-security-web:5.1.5.RELEASE",
			"io.opencensus:opencensus-api:${openCensusVersion}",
			"io.opencensus:opencensus-impl:${openCensusVersion}",
			"io.prometheus:simpleclient_httpserver:${prometheusClientVersion}",
			"io.opencensus:opencensus-exporter-stats-prometheus:${openCensusVersion}",
			"io.opentelemetry:opentelemetry-proto:0.3.0",
			"io.opentelemetry:opentelemetry-exporters-jaeger:0.3.0",
			"io.opentelemetry:opentelemetry-sdk:0.3.0",
			"io.grpc:grpc-netty:1.+",
			"com.google.protobuf:protobuf-java-util:3.+",
			'com.maxmind.geoip2:geoip2:2.12.0',
			'commons-net:commons-net:3.3',
			'org.springframework.boot:spring-boot-starter-actuator',
            "rocks.inspectit:opencensus-influxdb-exporter:1.1",
			"org.influxdb:influxdb-java:2.15"
	)
	compileOnly "org.projectlombok:lombok:1.18.4"
	annotationProcessor "org.projectlombok:lombok:1.18.4"
	testImplementation(
			project(':inspectit-ocelot-config'),
			"org.springframework.boot:spring-boot-starter-test",
			"io.opencensus:opencensus-impl:${openCensusVersion}",
			'org.springframework:spring-test:5.1.3.RELEASE',
			'org.apache.httpcomponents:httpclient:4.5.6',
			'org.mockito:mockito-core:2.23.4',
			'org.junit.jupiter:junit-jupiter-api:5.3.1',
			'org.mockito:mockito-junit-jupiter:2.23.0'
	)
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}
