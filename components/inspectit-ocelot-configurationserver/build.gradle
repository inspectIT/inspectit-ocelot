import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
    id("com.palantir.docker") version "${comPalantirDockerVersion}"
    alias(configServerLibs.plugins.orgSpringframeworkBoot)
    alias(libs.plugins.orgCyclonedxBom)
    alias(configServerLibs.plugins.ioSpringDependencyManagement)
}

apply plugin: 'idea'
apply plugin: 'java'

def serverMainClass = 'rocks.inspectit.ocelot.ConfigurationServer'

bootJar {
    archiveBaseName = 'inspectit-ocelot-configurationserver-noui'
    archiveVersion = "${buildVersion}"

    mainClass = "${serverMainClass}"
}

/**
 * Builds the configuration server and the web frontend.
 */
tasks.register('bootJarWithFrontend', BootJar) {
    group = "build"
    archiveBaseName = 'inspectit-ocelot-configurationserver'
    archiveVersion = "${buildVersion}"

    from("${project.projectDir}/../inspectit-ocelot-configurationserver-ui/out") {
        into('static/ui')
    }

    getTargetJavaVersion().set(JavaVersion.current())
    mainClass = "${serverMainClass}"

    with bootJar
}
bootJarWithFrontend.dependsOn ':inspectit-ocelot-configurationserver-ui:buildFrontend'

cyclonedxBom {
    includeConfigs = ["runtimeClasspath"]
    // Specifies the version of the CycloneDX specification to use.
    schemaVersion = "1.4"
    // Specifies the type of project being built. Defaults to 'library'
    projectType = "application"
    outputName = "bom"
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}


dependencies {
    compileOnly libs.orgProjectlombokLombok
    annotationProcessor libs.orgProjectlombokLombok
}

dependencies {
    implementation(
            project(':inspectit-ocelot-config'),
            // this is necessary as inspectit-ocelot-config needs it, but can
            // only use a compile-only (see details over there)
            libs.ioOpentelemetryOpentelemetrySdkMetrics,
            project(':inspectit-ocelot-configdocsgenerator'),
            configServerLibs.orgSpringframeworkBootSpringBootStarterWeb,
            configServerLibs.orgSpringframeworkBootSpringBootStarterDataJpa,
            configServerLibs.orgSpringframeworkBootSpringBootStartervalidation,
            configServerLibs.orgSpringframeworkSecuritySpringSecurityWeb,
            configServerLibs.orgSpringframeworksecuritySpringSecurityConfig,
            configServerLibs.orgSpringframeworkLdapSpringLdapCore,
            configServerLibs.orgSpringframeworkSecuritySpringSecurityLdap,
            configServerLibs.orgSpringframeworkBootSpringBootStarterActuator,

            configServerLibs.chQosLogbackLogbackClassic,
            configServerLibs.chQosLogbackLogbackCore,

            configServerLibs.orgYamlSnakeyaml,

            libs.orgApacheHttpcomponentsHttpclient, //Required for PATCH-Requests

            configServerLibs.orgXerialSqliteJdbc,
            configServerLibs.orgHibernateOrmHibernateCommunityDialects,
            configServerLibs.bundles.jsonwebtoken,
            configServerLibs.commonsIo,
            configServerLibs.orgApacheCommonsCommonsLang3,
            configServerLibs.orgFlywaydbFlywayCore,
            configServerLibs.bundles.jgit,
            configServerLibs.comGoogleCodeGson,

            // swagger
            libs.orgSpringdocSpringdocOpenapiUi,

            configServerLibs.comGoogleGuava
    )
    testImplementation(
            configServerLibs.orgSpringframeworkBootSpringBootStarterTest,
            configServerLibs.orgSpringframeworkSecuritySpringSecurityTest,
            configServerLibs.orgMockitoMockitoJunitJupiter,
            configServerLibs.comH2databaseH2,
            configServerLibs.orgJunitJupiterJunitJupiter,
            configServerLibs.orgJunitJupiterJunitJupiterApi,
            configServerLibs.orgAwaitility
    )
    testRuntimeOnly configServerLibs.orgJunitJupiterJunitJupiterEngine
}

tasks.register('copyServerJar', Copy) {
    dependsOn bootJarWithFrontend
    from("${buildDir}/libs/inspectit-ocelot-configurationserver-${version}.jar")
    into("${buildDir}/docker-jar")
    rename("inspectit-ocelot-configurationserver-${version}\\.jar",
            'inspectit-ocelot-configurationserver.jar')
}

docker {
    name "inspectit/inspectit-ocelot-configurationserver"
    dependsOn copyServerJar

    tag 'versioned', "hub.docker.com/${name}:${version}"
    dockerfile file('docker/Dockerfile')
    files 'docker/entrypoint.sh', "$buildDir/docker-jar/inspectit-ocelot-configurationserver.jar"
}
