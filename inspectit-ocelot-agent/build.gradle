import org.apache.tools.ant.taskdefs.condition.Os
import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    alias(libs.plugins.meChampeauJmh)
    id 'maven-publish'
    alias(libs.plugins.orgSpringframeworkBoot) apply false
    alias(libs.plugins.ioSpringDependencyManagement)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

evaluationDependsOn(':inspectit-ocelot-bootstrap')
evaluationDependsOn(':inspectit-ocelot-core')

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

group = 'rocks.inspectit.ocelot'

java {
    sourceCompatibility = 1.8 // Java version compatibility to use when compiling Java source
    targetCompatibility = 1.8 // Java version to generate classes for
}

configurations {
    opentelemetry
}

dependencies {
    compileOnly(
            project(':inspectit-ocelot-bootstrap'),
            libs.orgProjectlombokLombok
    )

    // dependencies for opentelemetry-fat.jar
    opentelemetry(
            libs.ioOpentelemetryOpentelemetrySdk,
            libs.ioOpentelemetryOpentelemetryOpencensusShim,
            libs.ioOpencensusOpencensusImpl,
            libs.ioOpentelemetryOpentelemetrySemconv,
            libs.ioOpentelemetryInstrumentationOpentelemetryResources,
            libs.comGoogleGuava
    )

    annotationProcessor libs.orgProjectlombokLombok

    testImplementation (
            libs.orgJunitJupiterJunitJupiterApi,
            libs.orgMockitoMockitoJunitJupiter,
            libs.chQosLogbackLogbackClassic,
            libs.chQosLogbackLogbackCore,
            libs.orgSlf4jJulToSlf4j,
            libs.orgSlf4jslf4jApi
    )
    testRuntimeOnly libs.orgJunitJupiterJunitJupiterEngine
}

// Copy the jar into the docker folder without version suffix
tasks.register('copyAgentJar', Copy) {
    dependsOn jar
    from(layout.buildDirectory.file("inspectit-ocelot-agent-${version}.jar"))
    into(layout.projectDirectory.dir("docker"))
    rename("inspectit-ocelot-agent-${version}\\.jar", 'inspectit-ocelot-agent.jar')
}

tasks.register('buildOpenTelemetryFatJar', Jar) {
    archiveFileName = "opentelemetry-fat.jar"
    destinationDirectory = layout.buildDirectory.dir("jarGen")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.opentelemetry.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processJmhResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('generateVersionFile') {
    ext.versionFile = layout.buildDirectory.file("ocelot-version.info").get().asFile
    doLast {
        ext.versionFile.withWriter('UTF-8') { writer ->
            writer << "$version"
        }
    }
}

jar {
    destinationDirectory = layout.buildDirectory
    archivesBaseName = "${project.name}"

    manifest {
        from file("src/main/resources/META-INF/MANIFEST.MF")
    }

    dependsOn generateVersionFile
    from generateVersionFile.versionFile

    // include the open-telemetry dependencies as a fat jar
    dependsOn buildOpenTelemetryFatJar
    from layout.buildDirectory.dir("jarGen")

    from project(':inspectit-ocelot-bootstrap').jar.outputs
    from project(':inspectit-ocelot-core').jar.outputs

    // include all "implementation" dependencies in fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

//----------------------------------------------------------------------------------------------------------------------
// Based on the integration tests of https://github.com/census-instrumentation/opencensus-java/tree/master/contrib/agent
//----------------------------------------------------------------------------------------------------------------------

sourceSets {
    systemTest {
        java {
            srcDir file('src/system-test/java')
        }
        resources.srcDir file('src/system-test/resources')
    }
}

configurations {
    systemTestCompile.extendsFrom testImplementation
    jmh.extendsFrom systemTestImplementation
}

dependencies {

    systemTestCompileOnly(
            platform(libs.ioOpentelemetryOpentelemetryBom),
            platform(libs.ioOpentelemetryOpentelemetryBomAlpha),
            libs.ioOpentelemetryOpentelemetryApi,
            libs.ioOpentelemetryOpentelemetrySdk,
            libs.ioOpentelemetryInstrumentationOpentelemetryResources,
            libs.ioOpentelemetryOpentelemetryOpencensusShim,
            libs.ioOpencensusOpencensusImpl,

            project(':inspectit-ocelot-bootstrap')
    )

    systemTestImplementation(
            libs.ioOpencensusOpencensusTesting,
            libs.ioOpencensusOpencensusApi,
            libs.comGoogleGuava,

            // for InMemorySpanExporter
            platform(libs.ioOpentelemetryOpentelemetryBom),
            libs.ioOpentelemetryOpentelemetrySdkTesting,
            libs.ioOpentelemetryOpentelemetryExporterLogging,
            platform(libs.ioOpentelemetryOpentelemetryBomAlpha),
            libs.ioOpentelemetryOpentelemetrySemconv,
            libs.ioOpentelemetryInstrumentationOpentelemetryResources,

            libs.orgJunitJupiterJunitJupiterEngine,
            libs.orgJunitJupiterJunitJupiterApi,

            libs.orgAssertjAssertjCore,
            libs.orgAwaitility,
            libs.comGithubTomakehurstWiremockJre8,
            libs.orgYamlSnakeyaml,

            // testing of apache client instrumentation
            // we still use the v4 client in system tests, since the default instrumentation does not work for v5
            libs.orgApacheHttpcomponentsHttpclient,
            // testing of the jms instrumentation
            libs.orgApacheActivemqApacheActivemq,
            // testing of jdbc instrumentation
            libs.comH2databaseH2,

            // only used for log-correlation tests
            libs.orgApacheLoggingLog4jLog4jApi,
            libs.orgApacheLoggingLog4jLog4jCore,
            libs.log4j,
            libs.chQosLogbackLogbackClassic, // Used for SLF4J Correlation
            libs.chQosLogbackLogbackCore,
            libs.orgSlf4jslf4jApi,
            libs.orgSlf4jJulToSlf4j
    )

    jmh(
            sourceSets.systemTest.output,
            project(':inspectit-ocelot-bootstrap')
    )

    constraints {
        systemTestImplementation(libs.comGoogleGuava) {
            because 'security issues'
        }
    }
}

if (!project.hasProperty("systemTestJavaHomes")) {
    ext.systemTestJavaHomes = System.properties['java.home']
}
def agentJarPath = jar.archiveFile.get().asFile.absolutePath

task systemTest {
    group = 'Verification'
    description = "Runs system tests with the agent attached as javaagent for all JDKs configured via the 'systemTestJavaHomes' property."

    // Always run this task when its executed
    outputs.upToDateWhen { false }

    def javaExecutables = ("${systemTestJavaHomes}")
            .tokenize(File.pathSeparator)
            .collect {
                Os.isFamily(
                        Os.FAMILY_WINDOWS)
                        ? "${it}/bin/java.exe"
                        : "${it}/bin/java"
            }
            .collect { new File(it).getCanonicalPath() }
            .unique()

    assert javaExecutables.size > 0:
            'No Java executables found for running system tests'

    javaExecutables.eachWithIndex { javaExecutable, index ->
        def paths = "${javaExecutable}".tokenize(File.separator)
        def javaName = "${index}"
        if (paths.size > 3) {
            javaName = paths[paths.size - 4] + "_" + paths[paths.size - 3]
        } else if (paths.length > 2) {
            javaName = paths[paths.size - 3]
        }

        def perVersionSystemTest = task("systemTest_${javaName}", type: Test) {

            group = 'Verification'
            description = "Runs system tests on ${javaExecutable}"


            useJUnitPlatform()

            testLogging {
                // Let Gradle output the stdout and stderr from tests, too. This is useful for investigating
                // test failures on Travis, where we can't view Gradle's test reports
                showStandardStreams = true

                // Include the exception message and full stacktrace for failed tests
                exceptionFormat 'full'
            }

            dependsOn jar

            testClassesDirs = sourceSets.systemTest.output.classesDirs
            classpath = sourceSets.systemTest.runtimeClasspath

            executable = javaExecutable

            jacoco {
                includeNoLocationClasses = true
                includes = ["rocks/inspectit/*"]
            }

            minHeapSize = "128m"
            maxHeapSize = "2048m"


            // The JaCoCo agent must be specified first so that it can instrument our agent.
            // This is a work around for the issue that the JaCoCo agent is added last, cf.
            // https://discuss.gradle.org/t/jacoco-gradle-adds-the-agent-last-to-jvm-args/7124.
            doFirst {

                def os = new ByteArrayOutputStream();
                exec {
                    executable = javaExecutable
                    args = ["-version"]
                    standardOutput = os
                    errorOutput = os
                }
                if (!os.toString().contains("\"1.8.")) {
                    //Turns the jigsaw warnings into errors, making the system tests fail in case of illegal accesses
                    jvmArgs "--illegal-access=deny"
                }

                // uncomment to check sources for classloading
                //jvmArgs "-verbose:class"

                jvmArgs "-Dinspectit.config.file-based.path=$projectDir/src/system-test/resources/config"
                // make inspectIT scan the workdir for configs
                jvmArgs "-Dinspectit.publishOpenTelemetryToBootstrap=true"  // make inspectIT push OTEL to the bootstrap
                jvmArgs jacoco.asJvmArg + ",inclbootstrapclasses=true"
                // JaCoCo agent first + bootstrap instrumentation
                jvmArgs "-javaagent:${agentJarPath}"  // Our agent second

                // stricter checker makes sure that {@link Scope}s are closed on the correct thread and
                // that they are not garbage collected before being closed
                jvmArgs "-Dio.opentelemetry.context.enableStrictContext=true"

                minHeapSize = "128m"
                maxHeapSize = "2048m"

                // enable reflection used in TestUtils#initializeOpenTelemetryForSystemTesting for JDK 11 and later
                if (JavaVersion.current() >= JavaVersion.VERSION_11) {
                    // --add-opens has the following syntax: {A}/{package}={B}
                    jvmArgs "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED"
                }

                jacoco.enabled = false  // Don't add the JaCoCo agent again.

                // stricter checker makes sure that {@link Scope}s are closed on the correct thread and
                // that they are not garbage collected before being closed.
                jvmArgs "-Dio.opentelemetry.context.enableStrictContext=true"

            }

            doFirst { logger.lifecycle("Running system tests using ${javaExecutable}") }
        }

        systemTest.dependsOn perVersionSystemTest
        systemTest.mustRunAfter test
    }
}

check.dependsOn systemTest

jmh {
    /**
     * Use -PjmhInclude='regular expression' to specify what tests to run with JMH.
     */
    if (project.hasProperty('jmhInclude')) {
        String regex = project.getProperty('jmhInclude')
        println "Filtering for JMH-Tests matching to regex: " + regex
        include = [regex]
    }

    /**
     * Use -jmhProfiler='profiler name and settings' to specify what profiler to run with JMH.
     */
    if (project.hasProperty('jmhProfiler')) {
        String profiler = project.getProperty('jmhProfiler')
        println "Using profiler for JMH tests: " + profiler
        profilers = [profiler]
    }

    /**
     * Use -jmhFlightRecorder=true to add Flight recorder options to the JVM args. You'll need openJDK 11 or OracleJDK 8.
     */
    def allJmvArgs = []
    if (project.hasProperty('jmhFlightRecorder')) {
        def recordingFile = "rec-jmh-${project.name}-${System.currentTimeMillis()}.jfr"
        def frProperties = "-XX:StartFlightRecording=duration=10s,delay=10s,filename=${recordingFile}".toString()
        allJmvArgs.addAll(['-XX:+UnlockCommercialFeatures', '-XX:+FlightRecorder', '-XX:+UnlockDiagnosticVMOptions', '-XX:+DebugNonSafepoints', frProperties, '-Djavax.xml.parsers.SAXParserFactory=com.sun.org.apache.xerces.internal.jaxp.SAXParserFactoryImpl'])
    }

    def loggingDir = layout.buildDirectory.dir("jmh/inspectit-logs")
    String loggingPath = "-Dinspectit.logging.file.path=${loggingDir.get().asFile.absolutePath}"
    String configPath = "-Dinspectit.config.file-based.path=$projectDir/src/jmh/resources/config".toString()
    String agentSetup = "-javaagent:${agentJarPath}".toString()

    allJmvArgs.addAll([loggingPath, configPath, agentSetup])
    jvmArgs = allJmvArgs

    // output of results and duplicateClassesStrategy
    humanOutputFile = layout.buildDirectory.file("jmh/human.txt").get()
    resultsFile = layout.buildDirectory.file("jmh/results.txt").get()
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE

    // shared settings if not defined on the class level
    warmupIterations = 3
    warmup = '3s'
    iterations = 5
    timeOnIteration = '3s'
    fork = 1
}
tasks.jmh.dependsOn jar
