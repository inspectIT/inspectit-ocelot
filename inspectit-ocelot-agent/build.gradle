import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    alias(libs.plugins.meChampeauJmh)
    id 'maven-publish'
    alias(libs.plugins.comPalantirDocker)
    alias(libs.plugins.orgSpringframeworkBoot) apply false
    alias(libs.plugins.ioSpringDependencyManagement)
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

evaluationDependsOn(':inspectit-ocelot-bootstrap')
evaluationDependsOn(':inspectit-ocelot-core')

test {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

group = 'rocks.inspectit.ocelot'

sourceCompatibility = 1.8 // Java version compatibility to use when compiling Java source.
targetCompatibility = 1.8 // Java version to generate classes for.


configurations {
    opentelemetry
}

dependencies {
    compileOnly(
            project(':inspectit-ocelot-bootstrap'),
            libs.orgProjectlombokLombok
    )

    opentelemetry(
        libs.ioOpentelemetryOpentelemetrySdk,
        libs.ioOpentelemetryOpentelemetryOpencensusShim,
        libs.ioOpencensusOpencensusImpl,
        libs.comGoogleGuava
    )
    annotationProcessor libs.orgProjectlombokLombok
    testImplementation (
            libs.orgJunitJupiterJunitJupiterApi,
            libs.orgMockitoMockitoJunitJupiter,
            libs.chQosLogbackLogbackClassic,
    )
    testRuntimeOnly libs.orgJunitJupiterJunitJupiterEngine

    constraints {
        opentelemetry(libs.comGoogleGuava) {
            because 'security issues'
        }
    }
}

task copyAgent(type: Copy) {
    dependsOn jar
    from(buildDir.toString() + "/inspectit-ocelot-agent-${version}.jar")
    into(buildDir)
    rename ("inspectit-ocelot-agent-${version}\\.jar", 'inspectit-ocelot-agent.jar')
}

docker {
    dependsOn copyAgent

    name "inspectit/inspectit-ocelot-agent"
    tag 'versioned', "hub.docker.com/${name}:${version}"
    dockerfile file('docker/Dockerfile')
    files 'docker/entrypoint.sh', "$buildDir/inspectit-ocelot-agent.jar"
}

task buildOpenTelemetryFatJar(type: Jar){
    archiveFileName = "opentelemetry-fat.jar"
    destinationDirectory = file("$buildDir/jarGen")
    from{
        configurations.opentelemetry.collect{it.isDirectory() ? it : zipTree(it) }
    }
}

processJmhResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    destinationDirectory = buildDir
    archivesBaseName = "${project.name}"

    manifest {
        from file("src/main/resources/META-INF/MANIFEST.MF")
    }

    // include the open-telemetry dependencies as a fat jar
    dependsOn buildOpenTelemetryFatJar
    from "$buildDir/jarGen"

    from project(':inspectit-ocelot-bootstrap').jar.outputs
    from project(':inspectit-ocelot-core').jar.outputs

    //include all "implementation" dependencies in fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

//----------------------------------------------------------------------------------------------------------------------
// Based on the integration tests of https://github.com/census-instrumentation/opencensus-java/tree/master/contrib/agent
//----------------------------------------------------------------------------------------------------------------------

sourceSets {
    systemTest {
        java {
            srcDir file('src/system-test/java')
        }
        resources.srcDir file('src/system-test/resources')
    }
}

configurations {
    systemTestCompile.extendsFrom testImplementation
    jmh.extendsFrom systemTestImplementation
}

dependencies {

    systemTestCompileOnly(
            platform(libs.ioOpentelemetryOpentelemetryBom),
            platform(libs.ioOpentelemetryOpentelemetryBomAlpha),
            libs.ioOpentelemetryOpentelemetrySdk,
            libs.ioOpentelemetryOpentelemetryOpencensusShim,
            libs.ioOpencensusOpencensusImpl,

            project(':inspectit-ocelot-bootstrap')
    )

    systemTestImplementation(
            libs.ioOpencensusOpencensusTesting,
            libs.ioOpencensusOpencensusApi,
            libs.comGoogleGuava,

            // for InMemorySpanExporter
            platform(libs.ioOpentelemetryOpentelemetryBom),
            libs.ioOpentelemetryOpentelemetrySdkTesting,
            libs.ioOpentelemetryOpentelemetryExporterLogging,
            platform(libs.ioOpentelemetryOpentelemetryBomAlpha),
            libs.ioOpentelemetryOpentelemetrySemconv,

            libs.orgJunitJupiterJunitJupiterEngine,
            libs.orgJunitJupiterJunitJupiterApi,

            libs.orgAssertjAssertjCore,
            libs.orgAwaitility,
            libs.comGithubTomakehurstWiremockJre8,
            libs.orgYamlSnakeyaml,

            libs.orgApacheHttpcomponentsHttpclient,
            // Switching to spring-boot dependency management excludes somehow magically the transitive
            // dependency commons-logging:commons-logging of org.apache.httpcomponents:httpclient.
            // Spring-Boot applications include org.springframework:spring-jcl, which provides a better
            // version of commons-logging
            // (see https://docs.spring.io/spring-framework/docs/5.3.25/javadoc-api/org/apache/commons/logging/package-summary.html)
            // In our special setup spring-jcl is not on the classpath, hence we need to add it manually.
            // For inspectit-ocelot-core it is pulled in via the manually declared spring-boot dependency.
            // For inspectit-configurationserver it is pulled in as it is a full blown spring-boot application.
            libs.orgSpringframeworkSpringJcl,
            libs.orgEclipseJettyJettyServer,
            libs.orgeclipseJettyJettyServlet,

            libs.comH2databaseH2,

            // only used for log-correlation tests
            libs.orgApacheLoggingLog4jLog4jApi,
            libs.orgApacheLoggingLog4jLog4jCore,
            libs.log4j,
            libs.chQosLogbackLogbackClassic // Used for SLF4J Correlation

    )

    jmh(
            sourceSets.systemTest.output,
            project(':inspectit-ocelot-bootstrap')
    )

    constraints {
        systemTestImplementation(libs.comGoogleGuava) {
            because 'security issues'
        }
    }
}

if (!project.hasProperty("systemTestJavaHomes")) {
    ext.systemTestJavaHomes = System.properties['java.home']
}
def agentJarPath = jar.archivePath.toString()

task systemTest {
    group = 'Verification'
    description = "Runs system tests with the agent attached as javaagent for all JDKs configured via the 'systemTestJavaHomes' property."

    // Always run this task when its executed
    outputs.upToDateWhen { false }

    def javaExecutables = ("${systemTestJavaHomes}")
            .tokenize(File.pathSeparator)
            .collect {
                org.apache.tools.ant.taskdefs.condition.Os.isFamily(
                        org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS)
                        ? "${it}/bin/java.exe"
                        : "${it}/bin/java"
            }
            .collect { new File(it).getCanonicalPath() }
            .unique()

    assert javaExecutables.size > 0:
            'No Java executables found for running system tests'

    javaExecutables.eachWithIndex { javaExecutable, index ->
        def paths = "${javaExecutable}".tokenize(File.separator)
        def javaName = "${index}"
        if (paths.size > 3) {
            javaName = paths[paths.size - 4] + "_" + paths[paths.size - 3]
        } else if (paths.length > 2) {
            javaName = paths[paths.size - 3]
        }

        def perVersionSystemTest = task("systemTest_${javaName}", type: Test) {

            group = 'Verification'
            description = "Runs system tests on ${javaExecutable}"


            useJUnitPlatform()

            testLogging {
                // Let Gradle output the stdout and stderr from tests, too. This is useful for investigating
                // test failures on Travis, where we can't view Gradle's test reports.
                showStandardStreams = true

                // Include the exception message and full stacktrace for failed tests.
                exceptionFormat 'full'
            }

            dependsOn jar

            testClassesDirs = sourceSets.systemTest.output.classesDirs
            classpath = sourceSets.systemTest.runtimeClasspath

            executable = javaExecutable

            jacoco {
                includeNoLocationClasses = true
                includes = ["rocks/inspectit/*"]
            }


            // The JaCoCo agent must be specified first so that it can instrument our agent.
            // This is a work around for the issue that the JaCoCo agent is added last, cf.
            // https://discuss.gradle.org/t/jacoco-gradle-adds-the-agent-last-to-jvm-args/7124.
            doFirst {

                def os = new ByteArrayOutputStream();
                exec {
                    executable = javaExecutable
                    args = ["-version"]
                    standardOutput = os
                    errorOutput = os
                }
                if (!os.toString().contains("\"1.8.")) {
                    //Turns the jigsaw warnings into errors, making the system tests fail in case of illegal accesses
                    jvmArgs "--illegal-access=deny"
                }

                jvmArgs "-Dinspectit.config.file-based.path=$projectDir/src/system-test/resources/config"
                // make inspectIT scan the workdir for configs
                jvmArgs "-Dinspectit.publishOpenTelemetryToBootstrap=true"  // make inspectIT push OTEL to the bootstrap
                jvmArgs jacoco.asJvmArg + ",inclbootstrapclasses=true"
                // JaCoCo agent first + bootstrap instrumentation
                jvmArgs "-javaagent:${agentJarPath}"  // Our agent second.

                // stricter checker makes sure that {@link Scope}s are closed on the correct thread and
                // that they are not garbage collected before being closed.
                jvmArgs "-Dio.opentelemetry.context.enableStrictContext=true"

                // enable reflection used in TestUtils#initializeOpenTelemetryForSystemTesting for JDK 11 and later
                if(JavaVersion.current() >= JavaVersion.VERSION_11) {
                    // --add-opens has the following syntax: {A}/{package}={B}
                    jvmArgs "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED"
                }

                jacoco.enabled = false  // Don't add the JaCoCo agent again.

                // stricter checker makes sure that {@link Scope}s are closed on the correct thread and
                // that they are not garbage collected before being closed.
                jvmArgs "-Dio.opentelemetry.context.enableStrictContext=true"

            }

            doFirst { logger.lifecycle("Running system tests using ${javaExecutable}.") }
        }

        systemTest.dependsOn perVersionSystemTest
    }
}
systemTest.mustRunAfter test
check.dependsOn systemTest

jmh {

    /**
     * Use -PjmhInclude='regular expression' to specify what tests to run with JMH.
     */
    if (project.hasProperty('jmhInclude')) {
        String regex = project.getProperty('jmhInclude')
        println "Filtering for JMH-Tests matching to regex: " + regex
        include = [regex]
    }

    /**
     * Use -jmhProfiler='profiler name and settings' to specify what profiler to run with JMH.
     */
    if (project.hasProperty('jmhProfiler')) {
        String profiler = project.getProperty('jmhProfiler')
        println "Using profiler for JMH tests: " + profiler
        profilers = [profiler]
    }

    /**
     * Use -jmhFlightRecorder=true to add Flight recorder options to the JVM args. You'll need openJDK 11 or OracleJDK 8.
     */
    def allJmvArgs = []
    if (project.hasProperty('jmhFlightRecorder')) {
        def recordingFile = "rec-jmh-${project.name}-${System.currentTimeMillis()}.jfr"
        def frProperties = "-XX:StartFlightRecording=duration=10s,delay=10s,filename=${recordingFile}".toString()
        allJmvArgs.addAll(['-XX:+UnlockCommercialFeatures', '-XX:+FlightRecorder', '-XX:+UnlockDiagnosticVMOptions', '-XX:+DebugNonSafepoints', frProperties, '-Djavax.xml.parsers.SAXParserFactory=com.sun.org.apache.xerces.internal.jaxp.SAXParserFactoryImpl'])
    }

    String loggingPath = "-Dinspectit.logging.file.path=$buildDir/jmh/inspectit-logs".toString()
    String configPath = "-Dinspectit.config.file-based.path=$projectDir/src/jmh/resources/config".toString()
    String agentSetup = "-javaagent:${agentJarPath}".toString()

    allJmvArgs.addAll([loggingPath, configPath, agentSetup])
    jvmArgs = allJmvArgs

    // output of results and duplicateClassesStrategy
    humanOutputFile = file("$buildDir/jmh/human.txt")
    resultsFile = file("$buildDir/jmh/results.txt")
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE

    // shared settings if not defined on the class level
    warmupIterations = 3
    warmup = '3s'
    iterations = 5
    timeOnIteration = '3s'
    fork = 1
}
tasks.jmh.dependsOn jar
