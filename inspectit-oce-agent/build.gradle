plugins {
    id "me.champeau.gradle.jmh" version "0.4.8"
}


dependencies {
    compileOnly(
            project(':inspectit-oce-bootstrap'),
    )
}

jar {
    destinationDir = buildDir
    archivesBaseName = "${project.name}"

    manifest {
        from file("src/main/resources/META-INF/MANIFEST.MF")
    }

    from project(':inspectit-oce-bootstrap').jar.outputs
    from project(':inspectit-oce-core').jar.outputs

    //include all "implementation" dependencies in fat jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

//----------------------------------------------------------------------------------------------------------------------
// Based on the integration tests of https://github.com/census-instrumentation/opencensus-java/tree/master/contrib/agent
//----------------------------------------------------------------------------------------------------------------------

sourceSets {
    systemTest {
        java {
            srcDir file('src/system-test/java')
        }
        resources.srcDir file('src/system-test/resources')
    }
}

configurations {
    systemTestCompile.extendsFrom testImplementation
    jmh.extendsFrom systemTestImplementation
}

dependencies {

    systemTestCompileOnly(
            "io.opencensus:opencensus-impl:${openCensusVersion}",
            project(':inspectit-oce-bootstrap')
    )

    systemTestImplementation(
            "io.opencensus:opencensus-testing:${openCensusVersion}",
            "io.opencensus:opencensus-api:${openCensusVersion}",
            
            'org.junit.jupiter:junit-jupiter-engine:5.3.1',
            'org.junit.jupiter:junit-jupiter-api:5.3.1',
            
            'org.assertj:assertj-core:3.11.1',
            'org.awaitility:awaitility:3.1.5',
            "com.github.tomakehurst:wiremock-jre8:2.21.0",

            'org.apache.httpcomponents:httpclient:4.5.6',
            'org.eclipse.jetty:jetty-server:9.4.3.v20170317',
            'org.eclipse.jetty:jetty-servlet:9.4.3.v20170317'
    )

    jmh(
        sourceSets.systemTest.output,
        project(':inspectit-oce-bootstrap')
    )
}

if (!project.hasProperty("systemTestJavaHomes")) {
    ext.systemTestJavaHomes = "$System.env.JAVA_HOME"
}
def agentJarPath = jar.archivePath.toString()

task systemTest {

    group = 'Verification'
    description = "Runs system tests with the agent attached as javaagent for all JDKs configured via the 'systemTestJavaHomes' property."

    def javaExecutables = ("${systemTestJavaHomes}")
            .tokenize(File.pathSeparator)
            .collect { org.apache.tools.ant.taskdefs.condition.Os.isFamily(
            org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS)
            ? "${it}/bin/java.exe"
            : "${it}/bin/java" }
    .collect { new File(it).getCanonicalPath() }
            .unique()

    assert javaExecutables.size > 0 :
            'No Java executables found for running system tests'

    javaExecutables.eachWithIndex { javaExecutable, index ->
        def paths = "${javaExecutable}".tokenize(File.separator)
        def javaName = "${index}"
        if(paths.size > 3) {
            javaName =  paths[paths.size - 4] + "_" + paths[paths.size - 3]
        } else if(paths.length > 2) {
            javaName = paths[paths.size - 3]
        }

        def perVersionSystemTest = task("systemTest_${javaName}", type: Test) {

            group = 'Verification'
            description = "Runs system tests on ${javaExecutable}"


            useJUnitPlatform()

            testLogging {
                // Let Gradle output the stdout and stderr from tests, too. This is useful for investigating
                // test failures on Travis, where we can't view Gradle's test reports.
                showStandardStreams = true

                // Include the exception message and full stacktrace for failed tests.
                exceptionFormat 'full'
            }

            dependsOn jar

            testClassesDirs = sourceSets.systemTest.output.classesDirs
            classpath = sourceSets.systemTest.runtimeClasspath

            executable = javaExecutable

            jacoco {
                includeNoLocationClasses = true
                includes = ["rocks/inspectit/*"]
            }

            // The JaCoCo agent must be specified first so that it can instrument our agent.
            // This is a work around for the issue that the JaCoCo agent is added last, cf.
            // https://discuss.gradle.org/t/jacoco-gradle-adds-the-agent-last-to-jvm-args/7124.
            doFirst {
                jvmArgs "-Dinspectit.config.file-based.path=$projectDir/src/system-test/resources/config"  // make inspectIT scan the workdir for configs
                jvmArgs jacoco.asJvmArg + ",inclbootstrapclasses=true"  // JaCoCo agent first + bootstrap instrumentaiton
                jvmArgs "-javaagent:${agentJarPath}"  // Our agent second.
                jacoco.enabled = false  // Don't add the JaCoCo agent again.
            }

            doFirst { logger.lifecycle("Running system tests using ${javaExecutable}.") }
        }

        systemTest.dependsOn perVersionSystemTest
    }
}
systemTest.mustRunAfter test
check.dependsOn systemTest

jmh {

    /**
     * Use -PjmhInclude='regular expression' to specify what tests to run with JMH.
     */
    if (project.hasProperty('jmhInclude')) {
        String regex = project.getProperty('jmhInclude')
        println "Filtering for JMH-Tests matching to regex: " + regex
        include = [regex]
    }

    /**
     * Use -jmhProfiler='profiler name and settings' to specify what profiler to run with JMH.
     */
    if (project.hasProperty('jmhProfiler')) {
        String profiler = project.getProperty('jmhProfiler')
        println "Using profiler for JMH tests: " + profiler
        profilers = [profiler]
    }

    /**
     * Use -jmhFlightRecorder=true to add Flight recorder options to the JVM args. You'll need openJDK 11 or OracleJDK 8.
     */
    def allJmvArgs = []
    if (project.hasProperty('jmhFlightRecorder')) {
        def recordingFile = "rec-jmh-${project.name}-${System.currentTimeMillis()}.jfr"
        def frProperties = "-XX:StartFlightRecording=duration=10s,delay=10s,filename=${recordingFile}".toString()
        allJmvArgs.addAll(['-XX:+UnlockCommercialFeatures', '-XX:+FlightRecorder', '-XX:+UnlockDiagnosticVMOptions', '-XX:+DebugNonSafepoints', frProperties, '-Djavax.xml.parsers.SAXParserFactory=com.sun.org.apache.xerces.internal.jaxp.SAXParserFactoryImpl'])
    }

    String loggingPath = "-Dinspectit.logging.file.path=$buildDir/jmh/inspectit-logs".toString()
    String configPath = "-Dinspectit.config.file-based.path=$projectDir/src/jmh/resources/config".toString()
    String agentSetup = "-javaagent:${agentJarPath}".toString()

    allJmvArgs.addAll([loggingPath, configPath, agentSetup])
    jvmArgs = allJmvArgs

    // output of results and duplicateClassesStrategy
    humanOutputFile = file("$buildDir/jmh/human.txt")
    resultsFile = file("$buildDir/jmh/results.txt")
    duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE

    // shared settings if not defined on the class level
    warmupIterations = 3
    warmup = '3s'
    iterations = 5
    timeOnIteration = '3s'
    fork = 1
}
tasks.jmh.dependsOn jar